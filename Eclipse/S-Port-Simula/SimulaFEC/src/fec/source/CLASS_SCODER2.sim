External class CLASS_SCODER1E;

CLASS_SCODER1E class CLASS_SCODER2;
%      -----------------------------------------------------------------
%      ---  COPYRIGHT 1988 by                                        ---
%      ---  Simula a.s.                                              ---
%      ---  Oslo, Norway                                             ---
%      ---                                                           ---
%      ---                 P O R T A B L E     S I M U L A           ---
%      ---              F R O N T - E N D    C O M P I L E R         ---
%      ---      S - C o d e    G e n e r a t o r     P a r t  2.     ---
%      ---                                                           ---
%      --- Selection switches: DKPIZ                                 ---
%      ---                                                           ---
%      ---    'D' Included when dump is wanted                       ---
%      ---    'K' Included always when quick capability wanted       ---
%      ---    'P' Included to make compiler use quick procedures     ---
%      ---    'I' Included to make compiler use inserted mnemonics   ---
%      ---    'Z' Included at system generation (for "ZIMULA")       ---
%      ---                                                           ---
%      -----------------------------------------------------------------
begin

%SPORT ON
%REUSE ON
%-Z %RECORD ON

%+I %INSERT $SIMSRC/fec/common.mne
%+I %INSERT $SIMSRC/fec/scoder.mne
%+I %INSERT $SPORTLIB/rtsinit.mne

codegenerator1e class codegenerator2;
begin short integer ll1,ll2;


%title ***  Procedure PROFILEGENERATOR  ***
      procedure profilegenerator(extq);ref(extquantity)extq;
      ! This procedure produces the routine profile corresponding
        to the declaration of an external non-Simula procedure;
      begin ref(quantity)qty,q; text it;
            short integer idlength,index,ttag;
            short integer language; boolean CP; character qtyp;
            
%-c         procedure invert; !*** invert the fpar list for C-procs;
%-c         begin ref(quantity) lastq, firstq, cq;
%-c               firstq:-extq.descr.fpar;
%-c               while lastq=/=firstq do begin
%-c                  cq:-firstq;
%-c                  while cq.next=/=lastq do cq:-cq.next;
%-c                  if lastq==none then extq.descr.fpar:-lastq:-cq
%-c                                 else lastq:-lastq.next:-cq;
%-c                  cq.next:-none;
%-c               end;
%-c         end;

            language:=extq.dim;
            CP:= language=rank(Cindex) or else
                 language=rank(Pascalindex);
            if extq.module==none then begin
               outbyte(vprofile);
               outtag(extq.ftag+profiledisp);
               outtext( s_external & "!0!!0!" );
               outtag(extq.ftag+bodydisp);
               it:-extq.symb.symbol;
               outbyte(it.length); outtext(it);
               it:-getBox(language).symbol;
               outbyte(it.length); outtext(it);
               it:-getBox(extq.virtno).symbol;
               outbyte(it.length); outtext(it);
               if language > rank(Libraryindex)
               then begin
                  !*** context parameter for Simuletta routines ***;
                  it:-copy("xxx _CONTEXT");
                  it.storechar(char(48 !rank('0'); + t_ref),0);
                                 !rtstypecode(rank(IREF)));
                  it.storechar(char(48 !rank('0'); + k_smp),1);
                  it.storechar(char(48 !rank('0'); + m_ref),2);
                  outtext( s_import & "!0!!0!" );
                  outtag(extq.ftag+contextdisp);
                  outbyte(it.length); outtext(it);
                  outTagWithIdent(ptoaddr);
               end context parameter;
%-c            if language=rank(Cindex) then invert;
               qty:-extq.descr.fpar;
               while qty =/= none do begin
                  inspect qty.symb do begin
                     idlength:=symbol.length;
                     if CP and then
                        (qty.kind=K_array or else qty.type=ITEXT)
                     then it:-"700 " & symbol
                     else begin
                        it:-blanks(4);
                        it.storechar(char(48 + rank(qty.type)), 0);
                                 !rtstypecode(rank(type));
                        it.storechar(char(48 + rank(qty.kind)), 1);
                        it.storechar(char(48 + rank(qty.categ)),2);
                        it:-it & symbol;
                     end;
                     outtext( s_import & "!0!!0!" );outtag(qty.ftag);
                     outbyte(it.length); outtext(it);
                     outTagWithIdent(if qty.kind= K_proc then ptraddr
                         else if qty.categ=C_name then ptgaddr
                         else if CP and then qty.categ=C_value and then
                                 (qty.type=ITEXT or else qty.type=IREF
                                  or else qty.kind=K_array)
                              then ptoaddr
                         else if qty.type= IREF or qty.type= IPTR
                               or qty.kind= K_array then ptoaddr
                         else if qty.kind= K_label then ptpaddr
                         else typetag(rank(qty.type))
                             );
                  end;
                  qty:-qty.next;
               end;
%-c            if language=rank(Cindex) then invert;
               if extq.type <> INOTY then begin
                  outbyte(vexport); outtag(extq.ftag+exportdisp);
                  outTagWithIdent(if CP and then extq.type=ITEXT
                           !*** C-routines export text as addr(chars) ;
                           then ptoaddr else typetag(rank(extq.type)));
               end;
               outbyte(vendprofile);
            end;
      end profilegenerator ;
%title ***  Procedure DESCRIPTORGENERATOR  ***

      procedure descriptorgenerator(q);ref(quantity)q;
      begin ref(quantity)qty;
            short integer ttag,firstnesttag,lastnesttag,repcnt;

            ! 'Descriptorgenerator' is called for each block, with q== ;
            ! declquant of the block. The purpose is to generate the   ;
            ! record descriptor for the block.                         ;
            begin
%+T				  STRC3("SCODER2",sourceline,"RECORD",ED_TAG(q.ftag+descriptordisp));
            	  outbyte(vrecord); outtag(q.ftag+descriptordisp);
                  if q.plev>one then begin
%+T					 STRC3("SCODER2",sourceline,"PREFIX",ED_TAG(q.prefqual.ftag+descriptordisp));
                     outbyte(vprefix);
                     outtag(q.prefqual.ftag+descriptordisp)
                  end
             else if q.kind <> K_record then begin
%+T					 STRC3("SCODER2",sourceline,"PREFIX",ED_TAG(if q.kind=K_proc and then q.type<>INOTY
%+T                     ! ??????  and then type <> IELSE ;
%+T                       then type_pro_tag(rank(q.type)) else Z_inst));
                       
                     outbyte(vprefix);
                     outTagWithIdent(if q.kind=K_proc and then q.type<>INOTY
                     ! ??????  and then type <> IELSE ;
                       then type_pro_tag(rank(q.type)) else Z_inst);
                  end;
                  qty:-q.descr.fpar;
                  while qty =/= none do begin
                     ttag:=getTypeTag(qty);
                     if ttag <> 0 then begin
%+T 					STRC3("SCODER2",sourceline,"   ATTR",ED_TAG(qty.ftag+quantdisp) & "  " & ED_TYPE(ttag));
                        outbyte(vattr); outTagWithIdent(qty.ftag+quantdisp);
                        outTagWithIdent(ttag);
                        if qty.kind=K_rep then begin
%+T						   STRC3("SCODER2",sourceline,"REP",edit(qty.descr.line1));
                           outbyte(vrep); out2byte(qty.descr.line1);
                        end;
                     end;
                     qty:-qty.next;
                  end;

                  ! Production of connests attributes of type OADDR;
                  ! For a procedure body connests will be zero,
                    it is counted in the procedure head;

                  lastnesttag:=q.descr.connests;
                  if lastnesttag <> 0 then begin
                     firstnesttag:=firstcontag(q);
                     lastnesttag:=lastnesttag+firstnesttag-1;
                     while firstnesttag <= lastnesttag do begin
                        outbyte(vattr);
                        outTagWithIdent(firstnesttag); outTagWithIdent(ptoaddr);
                        firstnesttag:=firstnesttag+1 end;
                  end;

                  if simob_descr then GenerateSequPPtags(q);
%+K               qty:-q.descr.fpar;
%+K               while qty=/=none do
%+K               begin if qty.kind=K_proc and then qty.categ=C_local
%+K                            and then qty.descr.thisused then begin
%+TK	 				   STRC3("SCODER2",sourceline,"ATTR",ED_TAG(qty.ftag+instrefdisp) & "  " & ED_TYPE(ptoaddr));
%+K                        outbyte(vattr); outtag(qty.ftag+instrefdisp);
%+K                        outTagWithIdent(ptoaddr) end;
%+K                     qty:-qty.next;
%+K               end;
%+T				  STRC2("SCODER2",sourceline,"ENDRECORD");
                  outbyte(vendrecord);
            end inspect SCF;
      end descriptorgenerator;
%title ***  Procedure PROTOTYPEGENERATOR  ***

      short integer noofprototypes;
            ! Counts total number of blocks for which a prototype is
              generated in the current S-Code module.  Is used to give
              each block a unique index into the resource-info vector
              of the module;

      procedure outprototypes(q);ref(quantity)q;
      ! Is called at block end for all blocks except classes.
        Is called  recursively for local classes.  Is also called at
        block end for a separately compiled class ;
      begin ref(quantity)qty,pqty;
            ref(brecord) lastC; !last removed const;
            qty:-q.descr.fpar;
            while qty =/= none do begin

            ! temporarily remove constants (if any) in order to
              simplify the prototype generator.
              The brecord of the constant is used as follows:
              - declquant == quantity of const
              - fpar      == quantity preceding const (in fpar list)
                             or NONE if the quantity is first
              - favirt    == quantity of the previous const removed
                             (i.e. chains the list of removed consts)
              Note that no changes are made in the quantities (except
              that remaining quantities may have their next-field chg.)
            ;
                  if qty.special = '!2!'  ! and then kind=K_ident ;
                  then begin
                     ! *** remove from fpar-list *** ;
                     if pqty==none then q.descr.fpar:-qty.next
                     else pqty.next:-qty.next;
                     !*** insert into rem-list ***;
                     inspect qty.descr do begin
                        fpar:-pqty;
                        favirt:- if lastC=/=none then lastC.declquant
                                 else none;
                        lastC:-this brecord;
                     end;
                  end
                  else begin
                       if qty.categ=C_local then begin
                        if qty.kind=K_class then outprototypes(qty) end;
                       pqty:-qty;
                  end;
                  qty:-qty.next;
            end;
            prototypegenerator(q);
            ! *** now regenerate fpar-list in SAME ORDER as before *** ;
            while lastC=/=none do begin
                  inspect lastC.fpar do next:-lastC.declquant
                  otherwise q.descr.fpar:-lastC.declquant;
                  inspect lastC.favirt do lastC:-descr
                  otherwise lastC:-none;
            end;
      end outprototypes;

      procedure prototypegenerator(q);ref(quantity)q;
      begin !*** Called from outprototypes only. Is called for each
            !*** block and generates the prototype etc. For a procedure,
            !*** it is called with the body as parameter. ;

            ref(quantity)qty;
      short integer navirt,       ! number of virtuals;
                    npnt,         ! no.of pointers ;
                    reppnt,       ! no. of repeated TEXT or REF ;
                    nrep,         ! total no. of repetitions ;
                    pntvec,       ! tag of pointer vector ;
                    rptvec,       ! tag of repeat vector ;
                    atrvirvec,    ! tag of attribute or virtual vector ;
                    ttag,         ! temp for tag evaluation;
                    pl;           ! for-variable for pref-level;
%+Z   short integer nconn;        ! connest ;
%-Z   character     nconn;        ! connest ;

% PQ        prior
            boolean procedure pointer(qty);ref(quantity)qty;
                  ! Return true if qty has to be represented
                    in the pointer vector of its enclosure ;
            if qty.categ=C_name then pointer:=true
            else begin
                  switch(NUL:K_max) qty.kind begin
                  when K_array do pointer:=true;
                  when K_ident do
                       pointer:= qty.type=IREF or else qty.type=ITEXT;
                  when K_proc,K_label,K_switch do
                       pointer:= qty.categ=C_unspec
%+K                  or else (qty.kind=K_proc and then qty.categ=C_local
%+K                           and then qty.descr.thisused)
                       ;
% PQ              when NONE do pointer:=false; !includes K_rep;
                  end case;
% PQ              else pointer:=false;
% PQ              qty:-none;
            end;
%page
% PQ  prior
      procedure count(qty);ref(quantity)qty;
      begin ref(quantity)q; boolean isnohead; short integer pct;
%           qty denotes the decl quantity, for a procedure called
%           first with qty == quantity of the body, then 
%           again for the head.  For a quick procedure called
%           only once, with qty==head.
%           Count number of elements in pointer vector and
%           in entity_list (simob info) by updating the
%           global variables npnt and nent.
% %+K       Also counts infixes by updating nrep and reppnt

      REP:  npnt:=npnt+qty.descr.connests;
            isnohead:=
                 if qty.kind=K_proc and then qty.plev<2 !Head quantity;
                 then false else simob_entity;

            q:-qty.descr.fpar;
            while q=/=none do begin
               if isnohead then nent:=nent+1;
               if q.kind = K_rep then begin
                  nrep:=nrep+1;
                  if q.type=IREF
                     or else q.type=ITEXT then reppnt:=reppnt+1;
               end else
               if pointer(q) then npnt:=npnt+1;
               q:-q.next;
            end while;

            if qty.kind=K_proc then begin if qty.plev=2 !body quantity ;
            then begin
                  if simob_entity then begin if qty.type <> INOTY
                  then nent:=nent+1 end;
                  if qty.type=IREF
                     or else qty.type=ITEXT then npnt:=npnt+1;
                  qty:-qty.prefqual; goto REP;
            end end;
% PQ        qty:-none;  !*** GC ***;
      end count;
%page
      procedure pntlist(qty); ref(quantity)qty;
      begin ref(quantity)q; short integer t,i,firstconntag,lastconntag;
            !*** Produce all AADDR-elements in the ptnvec of qty:
            !*** candidates are locals and parameters, connect pointers,
            !*** and the possible result attribute of a type procedure.
            !*** Connection pointers must be first in pntvec ***;
            !*** Repetitions are NOT registered here (but in repvec);

            begin

               ! ptnvec elements for connection pointers: ;
               ! (for procedures connests are counted in the
                  procedure head, and not in the body) ;
               if qty.descr.connests <> zero then begin
                  firstconntag:=firstcontag(qty);
                  lastconntag:=firstconntag+qty.descr.connests-1;
                  while firstconntag <= lastconntag do begin
                      outbyte(vc_aaddr); outTagWithIdent(firstconntag);
                      firstconntag:=firstconntag+1 end;
               end;

               if qty.kind=K_proc then begin if qty.plev=2 !body ;
               then begin ! Treat the procedure head first: ;
                  pntlist(qty.prefqual);
                  if qty.type=IREF then begin
%+T						STRC3("SCODER2",sourceline,"","C-AADDR " & ED_TTAG(RTS_tmp));          
                    	outtext( s_c_aaddr & RTS_refPro_val )
             end else if qty.type=ITEXT then begin
%+T						STRC3("SCODER2",sourceline,"","C-DOT " & ED_TTAG(RTS_txtPro_val));          
%+T						STRC3("SCODER2",sourceline,"","C-AADDR " & ED_TTAG(RTS_txtqnt_ent));          
             			outtext( s_c_dot   & RTS_txtPro_val  &
                                 s_c_aaddr & RTS_txtqnt_ent  );
             end end end treatment of procedure body ;

               q:-qty.descr.fpar;
               while q =/= none do begin
                  if q.kind=K_rep then !skip repetitions; else
%+K               if (if q.kind=K_proc and q.categ=C_local then
%+K                             q.descr.thisused else false)
%+K                  ! quick proc, called with head quantity ;
%+K                  ! describe its instref as a ref attribute;
%+K               then begin outbyte(vc_aaddr);
%+K                    outtag(q.ftag+instrefdisp) end
%+K               else
                  if pointer(q) then begin
                     if q.categ <> C_name and then
                        (q.kind=K_array or else
                        (q.kind=K_ident and then q.type=IREF)  )
                     then begin
%+T						   STRC3("SCODER2",sourceline,"","C-AADDR " & ED_TAG(q.ftag+quantdisp));          
                     	   outbyte(vc_aaddr);
                           outtag(q.ftag+quantdisp) end
                     else begin
%+T						STRC3("SCODER2",sourceline,"","C-DOT " & ED_TAG(q.ftag+quantdisp));          
                        outbyte(vc_dot); outtag(q.ftag+quantdisp);
                        if q.categ=C_name then begin
%+T							STRC3("SCODER2",sourceline,"","C-AADDR " & ED_TTAG(RTS_parqnt_ent));          
                            outtext( s_c_aaddr & RTS_parqnt_ent )
                        end else if q.kind=K_ident then begin      ! type=ITEXT ;
%+T							STRC3("SCODER2",sourceline,"","C-AADDR " & ED_TTAG(RTS_txtqnt_ent));          
                            outtext( s_c_aaddr & RTS_txtqnt_ent )
                        end else if q.categ=C_unspec then begin
                        
%+T                   	    if q.kind=K_proc then
%+T								 STRC3("SCODER2",sourceline,"","C-AADDR " & ED_TTAG(RTS_proqnt_sl))        
%+T                         else if q.kind=K_label then
%+T								 STRC3("SCODER2",sourceline,"","C-AADDR " & ED_TTAG(RTS_labqnt_sl))        
%+T							else STRC3("SCODER2",sourceline,"","C-AADDR " & ED_TTAG(RTS_swtqnt_sl));          
                        
                        
                      	    outtext( if q.kind=K_proc
                                then   ( s_c_aaddr & RTS_proqnt_sl )
                                    else if q.kind=K_label
                                then   ( s_c_aaddr & RTS_labqnt_sl )
                                else   ( s_c_aaddr & RTS_swtqnt_sl ) );
                        end
                     end;
                  end pointer;
                  q:-q.next;
               end;
            end inspect SCF;
      end pntlist;

      procedure replist(qty); ref(quantity) qty;
      begin ref(quantity)q; short integer i;
            !*** Produce all repdes-elements in the rptvec of qty:
            !*** candidates are locals (and parameters, not implemented)
            !*** REF and TEXT infixes must be first in rptvec ***;
            !*** - because these are treated by GC            ***;

            procedure outRepdesConst(q); ref(quantity) q;
            begin
                outtext( s_c_record  & RTS_repdes        &
                             s_attr  & RTS_repdes_nelt   &
                                       tag_int & s_c_int );
%+D             if q.descr==none or else q.descr.line1<=0
%+D             then internerr('!13!',sourceline);
                outinteger(q.descr.line1-1); !must be max index here!!!;
                outtext(     s_attr  & RTS_repdes_type   &
                                       tag_int & s_c_int );
                outsint(rank(q.type));
                outtext(     s_attr  & RTS_repdes_fld    &
                                       tag_aaddr & s_c_aaddr );
                outtag(q.ftag+quantdisp);
                outbyte( vendrecord );
            end;
            !*** first extract REF and TXT repetitions (for GC) ***;
            q:-qty.descr.fpar;
%+D         i:=0;
            while q =/= none do begin
               if q.kind=K_rep then begin
                  if q.type=ITEXT or else q.type=IREF then begin
%+D                  i:=i+1;
                     outRepdesConst(q);
               end end;
               q:-q.next;
            end;
%+D         if i<>reppnt then internerr('!13!',sourceline);
            !*** now include all other repetitions (for SIMOB) ***;
            q:-qty.descr.fpar;
            while q =/= none do begin
               if q.kind=K_rep then begin
                  if q.type=ITEXT or else q.type=IREF then !nothing;
                  else begin
%+D                  i:=i+1;
                     outRepdesConst(q);
               end end;
               q:-q.next;
            end;
%+D         if i<>nrep then internerr('!13!',sourceline);
      end replist;
%page
% P   prior
      procedure paramspeclist(q); ref(quantity) q;
      begin short integer i,ttag;
            short integer array atrdestag(0:npar); !zero never used;
            ref(quantity) qty;

            !*** Generate Parameter Vector: first (ref)atrdes      ***;
            !*** constants for all parameters, then an atrvec      ***;
            !*** constant refering to these.                       ***;
            !***                                                   ***;
            !*** We are in the procedure body: the parameters are  ***;
            !*** recorded in the procedure head (its fpar list     ***;
            !*** contains the parameters, and nothing but the      ***;
            !*** parameters).                                      ***;
         begin
            if simob_entity then SetParamTags(q);
            qty:-q.prefqual.descr.fpar; ! Get parameter list;
            i:= 1;
            while i<=npar do begin
                  if qty.categ<>C_name
%-T                  	 then atrdestag(i):=newTag
%+T                  	 then atrdestag(i):=newTTag( q.ident & "_par" & edit(i) )
                  else atrdestag(i):=qty.ftag+atrdesdisp;
                  
%+T               if qty.type=IREF or else qty.type=IPTR then begin
%+T			   			STRC3("SCODER2",sourceline,"CONST",ED_TAG(atrdestag(i)) & ED_TTAG(RTS_refdes) & " C-RECORD " & ED_TTAG(RTS_refdes) ); 
%+T						if  simob_entity then
%+T							 STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_atrdes_ident) & "  " & ED_TTAG(tag_oaddr) & "  C-OADDR " & ED_TAG(qty.symb.idtag))
%+T						else STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_atrdes_ident) & "  " & ED_TTAG(tag_oaddr) & "  ONONE")
%+T               end else begin
%+T			   			STRC3("SCODER2",sourceline,"CONST",ED_TAG(atrdestag(i)) & ED_TTAG(RTS_atrdes) & " C-RECORD " & ED_TTAG(RTS_atrdes) ); 
%+T						if  simob_entity then
%+T							 STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_atrdes_ident) & "  " & ED_TTAG(tag_oaddr) & "  C-OADDR " & ED_TAG(qty.symb.idtag))
%+T						else STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_atrdes_ident) & "  " & ED_TTAG(tag_oaddr) & "  ONONE")
%+T               end;
                  
                  outbyte(vconst); outTagWithIdent(atrdestag(i));
                  outtext( if qty.type=IREF or else qty.type=IPTR
                  then   ( RTS_refdes &
                              s_c_record & RTS_refdes     &
                                  s_attr & RTS_atrdes_ident &
                                               tag_oaddr  )
                  else   ( RTS_atrdes &
                              s_c_record & RTS_atrdes     &
                                  s_attr & RTS_atrdes_ident &
                                               tag_oaddr  ) );
                  if simob_entity then begin
                     outbyte(vc_oaddr); outTagWithIdent(qty.symb.idtag); end
                  else outbyte(vonone);

%+T				  STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_atrdes_fld) & "  " & ED_TTAG(tag_aaddr) & "  C-AADDR " & ED_TAG(qty.ftag+quantdisp));
                  outtext( s_attr & RTS_atrdes_fld & tag_aaddr & s_c_aaddr );
                  outTagWithIdent(qty.ftag+quantdisp);

%+T				  STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_atrdes_mode) & "  " & ED_TTAG(tag_int) & "  C-INT categ=" & edit(rank(qty.categ)));
                  outtext( s_attr & RTS_atrdes_mode & tag_int & s_c_int & "!1!" );
                  outbyte(rank(qty.categ)+48);

%+T				  STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_atrdes_kind) & "  " & ED_TTAG(tag_int) & "  C-INT kind=" & edit(rank(qty.kind)));
                  outtext( s_attr & RTS_atrdes_kind & tag_int & s_c_int & "!1!" );
%+D               if qty.kind > K_record then internerr('!13!',sourceline);
                  outbyte(rank(qty.kind)+48);

%+T				  STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_atrdes_type) & "  " & ED_TTAG(tag_int) & "  C-INT type=" & edit(rank(qty.type)));
                  outtext( s_attr & RTS_atrdes_type & tag_int & s_c_int & "!1!" );
                  outbyte(if qty.type>=IVALU then 48 else rank(qty.type)+48);
                  if qty.type=IREF or else qty.type=IPTR then begin
                     ! refatrdes attributes describing prefqual;
                     ! First comes (rbl,con) which are intended
                       for transplantation check.  The enclosure
                       of the qualifying class (prefqual.encl)
                       is of interest. rbl is its run time block
                       level relative to the procedure of which
                       qty is a parameter (encl) ;
%  *******  dropped - no transplantation check implemented *********
%  dropped           outtext(
%  dropped                 s_attr & RTS_refdes_rbl &
%  dropped                 tag_int & s_c_int );
%  dropped           outsint(rank(encl.rtblev)
%  dropped                    - rank(prefqual.encl.rtblev));

%  dropped           outtext( s_attr & RTS_refdes_con & tag_aaddr);
%  dropped           if prefqual.encl.inspected<>NUL
%  dropped           then begin
%  dropped              outbyte(vc_aaddr);
%  dropped              ttag:=conpointtag(prefqual.encl); outTagWithIdent(ttag)
%  dropped           end else outbyte(vanone);

%+T				     STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_refdes_qal) & "  " & ED_TTAG(tag_oaddr) & "  C-OADDR " & ED_TAG(qty.prefqual.ftag+prototypedisp));
                     outtext( s_attr & RTS_refdes_qal & tag_oaddr & s_c_oaddr  );
                     outtag(qty.prefqual.ftag+prototypedisp);
                  end qty.type=IREF;
%+T				  STRC2("SCODER2",sourceline,"ENDRECORD");
                  outbyte(vendrecord);       ! end (ref)atrdes ;

                  qty:-qty.next;
                  i:=i+1;
            end while;

            ! Generation of Attribute Vector for the parameters;
%-T         atrvirvec:=newTag
%+T         atrvirvec:=newTTag( q.ident & "_atrvirvec" )

             outbyte(vconst); outTagWithIdent(atrvirvec);
             if npar=0 then begin
%+T				STRC3("SCODER2",sourceline,"CONST",ED_TAG(atrvirvec) & "  " & ED_TTAG(RTS_atrvec) & "  FIXREP 0");
%+T				STRC3("SCODER2",sourceline,"C-RECORD",ED_TTAG(RTS_atrvec));
%+T				STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_atrvec_natr) & "  " & ED_TTAG(tag_int) & "  C-INT 0");
%+T				STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_atrvec_atr) & "  " & ED_TTAG(tag_oaddr));
                outtext( RTS_atrvec & s_fixrep & "!0!!0!" &
                         s_c_record & RTS_atrvec &
                         s_attr & RTS_atrvec_natr &
                         tag_int & s_c_int & "!1!0" &
                         s_attr & RTS_atrvec_atr & tag_oaddr  )  -- MYH: ONONE ???
             end else begin
%+T				STRC3("SCODER2",sourceline,"CONST",ED_TAG(atrvirvec) & "  " & ED_TTAG(RTS_atrvec) & "  FIXREP " & edit(npar));
%+T				STRC3("SCODER2",sourceline,"C-RECORD",ED_TTAG(RTS_atrvec));
%+T				STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_atrvec_natr) & "  " & ED_TTAG(tag_int) & "  C-INT " & edit(npar));
                outtext( RTS_atrvec & s_fixrep );     out2byte(npar);
                outtext( s_c_record & RTS_atrvec &
                         s_attr & RTS_atrvec_natr &
                         tag_int & s_c_int       );
                outsint(npar);
%+T				STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_atrvec_atr) & "  " & ED_TTAG(tag_oaddr) & "  -- Value(s) on following lines");
                outtext( s_attr & RTS_atrvec_atr & tag_oaddr  );
             end;

             for i:=1 step 1 until npar do begin
%+T				  STRC3("SCODER2",sourceline,"","  C-OADDR  " & ED_TAG(atrdestag(i)));
                 outbyte(vc_oaddr); outTagWithIdent(atrdestag(i))
             end;
%+T			 STRC2("SCODER2",sourceline,"ENDRECORD");
             outbyte(vendrecord);         ! end atrvec ;
          end inspect SCF;
      end paramspeclist;
%page

      noofprototypes:=noofprototypes+1;

      begin
         nconn:= if q.kind=K_proc and then q.plev=2 !proc.body, use head;
                 then q.prefqual.descr.connests else q.descr.connests;
         npar:=q.descr.npar; navirt:=q.descr.navirt;
         npnt:=nent:=reppnt:=nrep:=0; count(q);

         xpp_tag:=0;
         if simob_descr then GenerateExtPP(q);

         if npnt > 0 then begin !*** Generation of pointer vector ***;
%-T         pntvec:=newTag;
%+T         pntvec:=newTTag(q.ident & "'pntvec");
%+T			STRC3("SCODER2",sourceline,"CONST",ED_TAG(pntvec) & "  " & ED_TTAG(RTS_pntvec) & "  FIXREP " & edit(npnt));
            outbyte(vconst); outTagWithIdent(pntvec);
            outtext( RTS_pntvec & s_fixrep ); out2byte(npnt);
            if nconn = zero then begin
%+T			   STRC3("SCODER2",sourceline,"C-RECORD",ED_TTAG(RTS_pntvec));
%+T			   STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_pntvec_ncon) & "  " & ED_TTAG(tag_int) & "  C-INT 0");
               outtext( s_c_record & RTS_pntvec        &
                            s_attr & RTS_pntvec_ncon   &
                            tag_int & s_c_int & "!1!0" &
                            s_attr & RTS_pntvec_npnt   &
                            tag_int & s_c_int          )
            end else begin
%+T			   STRC3("SCODER2",sourceline,"C-RECORD",ED_TTAG(RTS_pntvec));
%+T			   STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_pntvec_ncon) & "  " & ED_TTAG(tag_int) & "  C-INT " & edit(nconn));
               outtext( s_c_record & RTS_pntvec        &
                            s_attr & RTS_pntvec_ncon   &
                            tag_int & s_c_int          );
               outsint(nconn);
               outtext(     s_attr & RTS_pntvec_npnt   &
                            tag_int & s_c_int          );
            end;
%+T			STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_pntvec_npnt) & "  " & ED_TTAG(tag_int) & "  C-INT " & edit(npnt));
            outsint(npnt);
%+T			STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_pntvec_pnt) & "  " & ED_TTAG(tag_aaddr) & "  ... list of pointers");
            outtext(     s_attr & RTS_pntvec_pnt & tag_aaddr);
              pntlist(q);
            outbyte(vendrecord);
%+T			STRC2("SCODER2",sourceline,"ENDRECORD");
         end generation of pointer vector;

         if nrep > 0 then begin !*** Generation of repeat vector ***;
%-T         rptvec:=newTag;
%+T 	   	rptvec:=newTTag("rptvec");
            outbyte(vconst); outTagWithIdent(rptvec);
            outtext( RTS_rptvec & s_fixrep ); out2byte(nrep);
            outtext( s_c_record & RTS_rptvec      &
                         s_attr & RTS_rptvec_npnt &
                         tag_int & s_c_int        ); outinteger(reppnt);
            outtext(     s_attr & RTS_rptvec_nrep &
                         tag_int & s_c_int        ); outinteger(nrep);
            outtext(     s_attr & RTS_rptvec_rep  & RTS_repdes );
              replist(q);
            outbyte(vendrecord);
%+T			STRC2("SCODER2",sourceline," +++++++++++++++++++++++ FORTSETT HER SEINERE !!!"); terminate_program; -- STOPP HER !!!!
         end generation of repeat vector;

         if q.kind=K_proc and then npar > 0
         then paramspeclist(q)

    else if navirt > 0 then begin !*** Generation of Virtual Vector ***;
%-T         atrvirvec:=newTag;
%+T 	   	atrvirvec:=newTTag("atrvirvec");
% +T			STRC2("SCODER2",sourceline," +++++++++++++++++++++++ FORTSETT HER SEINERE !!!"); terminate_program; -- STOPP HER !!!!
%+T			STRC3("SCODE2",sourceline,"CONST",ED_TAG(atrvirvec) & " " & ED_TTAG(RTS_virvec) & "FIXREP " & edit(navirt));
%+T			STRC3("SCODE2",sourceline,"C-RECORD",ED_TTAG(RTS_virvec));
%+T			STRC3("SCODE2",sourceline,"  ATTR",ED_TTAG(RTS_virvec_nvir) & " INT C-INT " & edit(navirt));
%+T			STRC3("SCODE2",sourceline,"  ATTR",ED_TTAG(RTS_virvec_vir) & " " & ED_TTAG(RTS_virdes));
            outbyte(vconst); outTagWithIdent(atrvirvec);
            outtext( RTS_virvec & s_fixrep ); out2byte(navirt);
            outtext( s_c_record & RTS_virvec     &
                         s_attr & RTS_virvec_nvir &
                         tag_int & s_c_int       );
            outsint(navirt);
            outtext(     s_attr & RTS_virvec_vir & RTS_virdes );
            qty:-q.descr.favirt;
            while qty =/= none do begin
%+T			   STRC3("SCODE2",sourceline,"  C-RECORD",ED_TTAG(RTS_virdes));
               outtext( s_c_record & RTS_virdes  );
               switch(NUL:K_max) qty.kind begin
               when K_proc do begin
                  outtext( s_attr & RTS_virdes_ppp & tag_oaddr  );
                  if qty.match==none then begin
%+T			     	   STRC3("SCODE2",sourceline,"    ATTR",ED_TTAG(RTS_virdes_ppp) & " OADDR ONONE");
                       outbyte(vonone)
                  end else begin
%+T				       STRC3("SCODE2",sourceline,"    ATTR",ED_TTAG(RTS_virdes_ppp) & " OADDR C-OADDR " & ED_TAG(qty.match.ftag+prototypedisp));
                  	   outbyte(vc_oaddr);
                       outtag(qty.match.ftag+prototypedisp);
                  end;
                  outtext( s_attr & RTS_virdes_qal & tag_oaddr  );
                  if qty.match==none or else qty.type <> IREF then begin
%+T					   STRC3("SCODE2",sourceline,"    ATTR",ED_TTAG(RTS_virdes_qal) & " OADDR ONONE");
                       outbyte(vonone)
                  end else begin
%+T			 		   STRC3("SCODE2",sourceline,"    ATTR",ED_TTAG(RTS_virdes_qal) & " OADDR C-OADDR " & ED_TAG(qty.match.prefqual.ftag+prototypedisp));
                       outbyte(vc_oaddr);
                       outtag(qty.match.prefqual.ftag+prototypedisp)
                  end
                  end;
               when K_label do begin
                  outtext( s_attr & RTS_virdes_pad & tag_paddr  );
                  if qty.match==none then begin
%+T					   STRC3("SCODE2",sourceline,"    ATTR",ED_TTAG(RTS_virdes_pad) & " PADDR NOWHERE");
                       outbyte(vnowhere)
                  end else begin
%+T			 		   STRC3("SCODE2",sourceline,"    ATTR",ED_TTAG(RTS_virdes_pad) & " PADDR C-PADDR " & ED_TAG(qty.match.ftag+quantdisp));
                       outbyte(vc_paddr);
                       outtag(qty.match.ftag+quantdisp) end;
                  end;
               when K_switch do begin
                  outtext( s_attr & RTS_virdes_des & tag_oaddr  );
                  if qty.match==none then begin
%+T					   STRC3("SCODE2",sourceline,"    ATTR",ED_TTAG(RTS_virdes_des) & " OADDR ONONE");
                       outbyte(vonone)
                  end else begin
%+T			 		   STRC3("SCODE2",sourceline,"    ATTR",ED_TTAG(RTS_virdes_pad) & " OADDR C-OADDR " & ED_TAG(qty.match.ftag+quantdisp));
                       outbyte(vc_oaddr);
                       outtag(qty.match.ftag+quantdisp) end;
                  end;
               when NONE do StopScode_1('!13!',sourceline); !wrong virtual;
               end case;

               qty:-qty.next;
%+T			   STRC2("SCODE2",sourceline,"  ENDRECORD");
               outbyte(vendrecord);    ! end virdes;
            end;
%+T			STRC2("SCODE2",sourceline,"ENDRECORD");
            outbyte(vendrecord);       ! end virvec;
         end;

         ! Generate record of appropriate prototype ;

         outbyte(vconst);
         if q.kind=K_proc then begin
%+T			STRC3("SCODER2",sourceline,"CONST",ED_TAG(q.prefqual.ftag+prototypedisp) & "  " & ED_TTAG(RTS_proPtp)); 
%+T		    STRC3("SCODER2",sourceline,"C-RECORD",ED_TTAG(RTS_proPtp)); 
--%+T		    STRC3("SCODER2",sourceline,"   ATTR0",ED_TTAG(RTS_ptp_refVec) & "  " & ED_TTAG(tag_oaddr) );
            outtag(q.prefqual.ftag+prototypedisp);
            outtext( RTS_proPtp & s_c_record & RTS_proPtp  &
                         s_attr & RTS_ptp_refVec & tag_oaddr  )
         end
         else begin
            outtag(q.ftag+prototypedisp);
            if q.kind=K_subbl then begin
%+T			   STRC3("SCODER2",sourceline,"CONST",ED_TAG(q.ftag+prototypedisp) & "  " & ED_TTAG(RTS_subPtp)); 
%+T			   STRC3("SCODER2",sourceline,"C-RECORD",ED_TTAG(RTS_subPtp)); 
               outtext( RTS_subPtp &
                        s_c_record & RTS_subPtp  &
                            s_attr & RTS_ptp_refVec & tag_oaddr  )
            end else begin ! kind=K_class/prefbl ;
%+T			   STRC3("SCODER2",sourceline,"CONST",ED_TAG(q.ftag+prototypedisp) & "  " & ED_TTAG(RTS_claPtp) & " FIXREP " & edit(q.plev+1)); 
               outtext( RTS_claPtp & s_fixrep ); out2byte(q.plev+1);
%+T			   STRC3("SCODER2",sourceline,"C-RECORD",ED_TTAG(RTS_claPtp)); 
--%+T		       STRC3("SCODER2",sourceline,"   ATTR0",ED_TTAG(RTS_ptp_refVec) & "  " & ED_TTAG(tag_oaddr) );
               outtext( s_c_record & RTS_claPtp  &
                            s_attr & RTS_ptp_refVec & tag_oaddr );
            end;
         end;

         if npnt<>0
         then begin
%+T		     STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_ptp_refVec) & "  " & ED_TTAG(tag_oaddr) & "  OADDR" & ED_TAG(pntvec));
         	outbyte(vc_oaddr); outTagWithIdent(pntvec)
         end else begin
%+T		     STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_ptp_refVec) & "  " & ED_TTAG(tag_oaddr) & "  ONONE"); 
--%+T			 STRC2("SCODER2",sourceline,"      ONONE"); 
         	 outbyte(vonone);
         end;	 

         if nrep<>0 then begin
%+T		    STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_ptp_repvec) & "  " & ED_TTAG(tag_oaddr) & "  OADDR" & ED_TAG(rptvec));
            outtext( s_attr & RTS_ptp_repvec & tag_oaddr );
            outbyte(vc_oaddr); outTagWithIdent(rptvec);
%+T		    STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_ptp_xpp) & "  " & ED_TTAG(tag_oaddr) ); --& "  OADDR" & ED_TAG(pntvec));
            outtext( s_attr & RTS_ptp_xpp & tag_oaddr  );
         end else begin

%+T	        STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_ptp_repvec) & "  " & ED_TTAG(tag_oaddr) & "  ONONE"); 
            outtext( s_attr & RTS_ptp_repvec & tag_oaddr & s_onone &
                     s_attr & RTS_ptp_xpp & tag_oaddr  );
         end;
         
         if simob_descr
         then begin
%+T	     STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_ptp_xpp) & "  " & ED_TTAG(tag_oaddr) & "  C-OADDR  " & ED_TAG(xpp_tag)); 
         	outbyte(vc_oaddr); outTagWithIdent(xpp_tag)
         end else begin
%+T	     STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_ptp_xpp) & "  " & ED_TTAG(tag_oaddr) & "  ONONE"); 
         	outbyte(vonone);
         end;

%+T	     STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_ptp_lng) & "  " & ED_TTAG(tag_size) & "  C-SIZE  " & ED_TAG(q.ftag+descriptordisp)); 
         outtext( s_attr & RTS_ptp_lng & tag_size & s_c_size );
         outtag(q.ftag+descriptordisp);

         if q.kind=K_subbl then begin
%+T	   		STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_subPtp_cnt) & "  " & ED_TTAG(tag_paddr) & "  C-PADDR  " & ED_TAG(q.ftag+cntdisp)); 
            outtext( s_attr & RTS_subPtp_cnt &
                     tag_paddr & s_c_paddr ); outtag(q.ftag+cntdisp);
         end
    else if q.kind=K_proc then begin
%+T	        STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_proPtp_start) & "  " & ED_TTAG(tag_paddr) & "  C-PADDR  " & ED_TAG(q.prefqual.ftag+startdisp)); 
            outtext( s_attr & RTS_proPtp_start &
                     tag_paddr & s_c_paddr   );
            outTagWithIdent(q.prefqual.ftag+startdisp);

            outtext( s_attr & RTS_proPtp_parVec & tag_oaddr  );
            if npar > 0
            then begin
%+T	           STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_proPtp_parVec) & "  " & ED_TTAG(tag_oaddr) & "  C-OADDR  " & ED_TAG(atrvirvec)); 
               outbyte(vc_oaddr); outTagWithIdent(atrvirvec) end
            else begin
%+T	           STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_proPtp_parVec) & "  " & ED_TTAG(tag_oaddr) & "  ONONE"); 
               outbyte(vonone);
            end;   
         end else begin
         ! kind=K_class/prefbl - NBNB: this depends on the values
           of dcl/stm/inr disp constants as x/x+1/x+2;
%+T	        STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_claPtp_plv) & "  " & ED_TTAG(tag_int) & "  C-INT  " & edit(q.plev-1)); 
            outtext( s_attr & RTS_claPtp_plv & tag_int & s_c_int  );
            outsint(q.plev-1);

            if q.descr.dcltag <> NUL then begin
%+T	           STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_claPtp_dcl) & "  " & ED_TTAG(tag_paddr) & "  C-PADDR  " & ED_TAG(q.ftag+dcldisp)); 
               outtext( s_attr    & RTS_claPtp_dcl & tag_paddr & s_c_paddr );
               outtag(q.ftag+dcldisp) end
            else begin
%+T	           STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_claPtp_dcl) & "  " & ED_TTAG(tag_paddr) & "  NOWHERE"); 
               outtext( s_attr    & RTS_claPtp_dcl & tag_paddr & s_nowhere );
            end;
            if q.descr.stmtag <> NUL then begin
%+T	           STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_claPtp_stm) & "  " & ED_TTAG(tag_paddr) & "  C-PADDR  " & ED_TAG(q.ftag+isorank(q.descr.stmtag))); 
               outtext( s_attr    & RTS_claPtp_stm & tag_paddr & s_c_paddr     );
               outtag(q.ftag+isorank(q.descr.stmtag));
            end else begin
%+T	           STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_claPtp_stm) & "  " & ED_TTAG(tag_paddr) & "  NOWHERE"); 
               outtext( s_attr    & RTS_claPtp_stm & tag_paddr & s_nowhere );
            end;              

            outtext( s_attr    & RTS_claPtp_cntInr & tag_paddr & s_c_paddr     );
            if q.descr.inrtag <> NUL then begin
%+T	           STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_claPtp_cntInr) & "  " & ED_TTAG(tag_paddr) & "  C-PADDR  " & ED_TAG(q.ftag+isorank(q.descr.inrtag))); 
               outtag(q.ftag+isorank(q.descr.inrtag));
            end else begin
%+T	           STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_claPtp_cntInr) & "  " & ED_TTAG(tag_paddr) & "  C-PADDR  " & ED_TAG(getINRtag(q))); 
               outINRtag(q);
            end;   

            if navirt>0 then begin
%+T	           STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_claPtp_virts) & "  " & ED_TTAG(tag_oaddr) & "  C-OADDR  " & ED_TAG(atrvirvec)); 
               outtext( s_attr    & RTS_claPtp_virts & tag_oaddr & s_c_oaddr     );
               outTagWithIdent(atrvirvec);
            end else begin
%+T	           STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_claPtp_virts) & "  " & ED_TTAG(tag_oaddr) & "  ONONE"); 
               outtext( s_attr    & RTS_claPtp_virts & tag_oaddr & s_onone       );
			end;
			   
%+T	        STRC3("SCODER2",sourceline,"   ATTR",ED_TTAG(RTS_claPtp_prefix) & "  " & ED_TTAG(tag_oaddr) & "  -- List on following lines"); 
            outtext( s_attr & RTS_claPtp_prefix & tag_oaddr  );

            ! Prefix chain generation ;
            ttag:=q.plev-1; !temp use of ttag;
            if ttag<>0 then begin
               qty:-q.prefqual;
               while qty.plev>one do begin
                     prefixchain(qty.plev):-qty;
                     qty:-qty.prefqual; end;
%+T	           STRC3("SCODER2",sourceline,"","  C-OADDR  " & ED_TAG(qty.ftag+prototypedisp)); 
               outbyte(vc_oaddr);
               outtag(qty.ftag+prototypedisp);
               for pl:=2 step 1 until ttag do begin
%+T	           	   STRC3("SCODER2",sourceline,"","  C-OADDR  " & ED_TAG(prefixchain(pl).ftag+prototypedisp)); 
                   outbyte(vc_oaddr);
                   outtag(prefixchain(pl).ftag+prototypedisp);
                   prefixchain(pl):-none;
               end;
            end plev>0;
            
%+T	        STRC3("SCODER2",sourceline,"","  C-OADDR  " & ED_TAG(q.ftag+prototypedisp)); 
%+T	        STRC3("SCODER2",sourceline,"","  ONONE"); 
            outbyte(vc_oaddr); outtag(q.ftag+prototypedisp);
            outbyte(vonone);
--%+T			   STRC2("SCODER2",sourceline," +++++++++++++++++++++++ FORTSETT HER SEINERE !!!"); terminate_program; -- STOPP HER !!!!

         end ICLAS or IBPRF;

%+T	     STRC2("SCODER2",sourceline,"ENDRECORD");
         outbyte(vendrecord);        ! end prototype ;

      end inspect SCF;

   end prototypegenerator;

%title ***  Procedure OUTDESCRIPTORS  ***

      !*** recursive ***;
      procedure outdescriptors(q);ref(quantity)q;
%           ! This procedure is called at "block begin" for all kinds of
%           brecords, except for classes and procedure heads.
%           It generates descriptors for all quantities which has to be
%           defined when code for the body of the brecord is generated ;

%           ! First outdescriptors is called recursively for a possible
%           prefix class, if it is not done yet.  Then the descriptors
%           for q are generated, including recursive calls on
%           outdescriptors for classes and procedure heads local to q ;

%           ! If q is external quantity then a routine profile is generated.
%           Otherwise the descriptors produced for q are:
%              - record descriptor for q.
%              - specifications of labels and switches local to q. These
%                are not represented by an attribute of the record
%                descriptor for q.  Labelspecs are generated for the
%                labels (including the labels which are contained in the
%                prototype: cnt/start/dcl/smt/cnt_inr).  Constspecs are
%                generated for the switch descriptors
%                NOTE june 88 pj: dcl/stm labels are not 'speced.
%              - specification of text entities of named text constants;

%               ! Generate constspecs for the prototype and its
%                 label attributes,if not external. The reason is:   ;
%               ! Prototypes are first generated at "block end" and
%                 the labels are first defined when the statements
%                 of the block is being compiled;
       
      begin ref(quantity)qty; text txt;
            if q.categ=C_extnal then begin
               if q qua extquantity.clf=Clf000 then begin
                  ! external SIMULA quantity ;
                  outbyte(vinsert);
%-Z %IMPLNONE OFF
                  inspect q qua extquantity.module do begin
%+T                  text iinfo;
                     txt:-nameof(modulhi,modullo);
%+T                  iinfo:-copy(txt);
                     outbyte(txt.length); outtext(txt);
                     txt:-nameof(checkhi,checklo);
%+T                  iinfo:-copy(iinfo & " " & txt);
                     outbyte(txt.length); outtext(txt);
                     txt:-if q.virtno<>0 then getBox(q.virtno).symbol
                          else "?";
                     !*** extid - if none, output ? ;
%+T                  iinfo:-copy(iinfo & " " & txt);
                     outbyte(txt.length); outtext(txt);
%+T					 STRC3("SCODER2",sourceline,"INSERT",iinfo & " " & ED_TAG(ftag) & " " & ED_TAG(ftag + exttagnum-1));
                     outTagWithIdent(ftag);
                     outTagWithIdent(ftag + exttagnum-1 );
                  end
               end
          else if q qua extquantity.clf=Clf001 then
                  ! external non-SIMULA quantity             ;
                  profilegenerator(q);
            end
       else switch(NUL:K_max) q.kind begin
            when K_subbl do begin
               descriptorgenerator(q);
%+T			   STRC3("SCODER2",sourceline,"CONSTSPEC",ED_TAG(q.ftag+prototypedisp) & "  " & ED_TTAG(RTS_subPtp));
%+T			   STRC3("SCODER2",sourceline,"LABELSPEC",ED_TAG(q.ftag+cntdisp));
               outbyte(vconstspec); outtag(q.ftag+prototypedisp);
               outtext( RTS_subPtp & s_labelspec );
               outtag(q.ftag+cntdisp);
            end;
            when K_proc  do begin
               descriptorgenerator(q);
               if q.plev<2 then begin
%+T				  STRC3("SCODER2",sourceline,"CONSTSPEC",ED_TAG(q.ftag+prototypedisp) & "  " & ED_TAG(Z_proPtp));
                  outbyte(vconstspec); outtag(q.ftag+prototypedisp);
%  not used?      outtext( RTS_pptp & s_labelspec );
%  not used?      outtag(q.ftag+startdisp);
                  outTagWithIdent(Z_proPtp);

                  ! generate constspecs for name params   ;
                  ! to this procedures, as they are used  ;
                  ! when calling the procedure            ;
                  qty:-q.descr.fpar;
                  while qty =/= none do begin
                     if qty.categ=C_name then begin
%+T						STRC3("SCODER2",sourceline,"CONSTSPEC",ED_TAG(qty.ftag+prototypedisp) & "  " & ED_TAG(if qty.type=IREF or else qty.type=IPTR then Z_refdes else Z_atrdes));
                        outbyte(vconstspec);
                        outTagWithIdent(qty.ftag+prototypedisp);
                        outTagWithIdent(if qty.type=IREF or else qty.type=IPTR then Z_refdes else Z_atrdes);
                     end;
                     qty:-qty.next;
                  end;
               end;
            end;
            when K_class, K_record do begin
               q.descr.descriptorpr:=true;
               if q.plev>one then begin
%-Z %IMPLNONE OFF
                if q.prefqual is quantity then begin
                 if not q.prefqual.descr.descriptorpr
                 then outdescriptors(q.prefqual) end end;

   DESCRGEN:   descriptorgenerator(q);
%+T			   STRC3("SCODER2",sourceline,"CONSTSPEC",ED_TAG(q.ftag+prototypedisp) & "  " & ED_TTAG(RTS_claPtp) & "  FIXREP " & edit(q.plev+1));
               outbyte(vconstspec); outtag(q.ftag+prototypedisp);
               outtext( RTS_claPtp & s_fixrep );
               out2byte(q.plev+1);
               if q.descr.inrtag <> NUL then begin
%+T				  STRC3("SCODER2",sourceline,"LABELSPEC",ED_TAG(q.ftag+isorank(q.descr.inrtag)));
                  outbyte(vlabelspec);
                  outtag(q.ftag+isorank(q.descr.inrtag));
               end;
            end;
            when K_prefbl do
               goto DESCRGEN;
            end case;

            q:-q.descr.fpar;
            while q =/= none do begin
               if q.kind=K_class and then q.descr.descriptorpr
               then !nothing; else
%-Z %IMPLNONE OFF
               if q is extquantity and then q.categ<>C_extnal
               then !nothing;
          else if (q.kind=K_proc or else q.kind=K_class or else
                   q.kind=K_record) and then
                  (q.categ=C_local or else q.categ=C_extnal)
               then outdescriptors(q)
          else if q.categ=C_local then begin
                  if q.kind=K_label then begin
                     if q.dim=0 then begin !no spec for forvar;
                        outbyte(vlabelspec); outtag(q.ftag+quantdisp);
                     end;
                  end
             else if q.kind=K_switch then begin
                     outbyte(vconstspec); outtag(q.ftag+quantdisp);
                     outtext( RTS_swtdes & s_fixrep );
                     out2byte(q.dim+1);
                  end
             else if q.special<>NUL then begin if q.kind=K_ident
                     then begin if q.type=ITEXT
                  then begin
                     outbyte(vconstspec); outTagWithIdent(q.ftag);
                     outTagWithIdent(Z_txtqnt);
                       !specify const addr of text entity;
                  end end end;
               end;
               q:-q.next;
            end;
      end outdescriptors;

%title *****   forwardGOTO   *****

%+PQ  prior
      procedure forwardGOTO;
      begin
         if nonewindex then begin !lay out stepping stone;
%+D         if option(rank('Y'))<>NUL then outtrace("step GOTO");
            while opq.dim > 0 do
             inspect opq.prevmeaning do begin
                opq.prevmeaning:-prevmeaning;
%-Z             outfdest(     this sembox qua stackedi.rehi );
%+Z             outfdest(rank(this sembox qua stackedi.rehi));
                opq.dim:=opq.dim-1 end;
         end;
         getnewindex; opq.dim:=opq.dim+1;
         outbyte(vfjump); outbyte(curindex);
%-Z      inspect new stackedi(     curindex ,NUL) do begin
%+Z      inspect new stackedi(char(curindex),NUL) do begin
           prevmeaning:-opq.prevmeaning; opq.prevmeaning:-this stackedi;
         end;
      end;

%title ***   addr/update/selectPARQNT - addrNAMEQNT   ***

%+PQ  prior
      procedure addrPARQNT; !*** opq implicit parameter;
                            ! opq.categ=C_name ;
      begin staticencl;
%+T			STRC3("SCODER2",sourceline,"REMOTE",ED_TAG(opq.ftag+quantdisp));
            outbyte(vremote);
            outtag(opq.ftag+quantdisp);
      end addrPARQNT;


%+PQ  prior
      procedure addrNAMEQNT; !*** opq implicit parameter;
      begin boolean save_emitted;
            if asd <> 0 then begin
%+D            if option(rank('Y'))<>NUL then outtrace("save-FNP_ACC");
%+T			   STRC_saveScheme;
               outtext( saveScheme  ); save_emitted:=true end
            else save_emitted:=false;
            addrPARQNT;
%+T			STRC2("SCODER2",sourceline,"DEREF");
%+T			STRC3("SCODER2",sourceline,"ASSCALL",ED_TTAG(RTS_b_fnp_acc1));
%+T			STRC3("SCODER2",sourceline,"CALL",ED_TTAG(RTS_b_fnp_acc2));
%+T			if save_emitted then STRC_restoreScheme;
            outtext( if save_emitted
              then ( s_deref   &
                     s_asscall & RTS_b_fnp_acc1  &
                     s_call    & RTS_b_fnp_acc2  &            !-1;
                     restoreScheme               )
              else ( s_deref   &
                     s_asscall & RTS_b_fnp_acc1  &
                     s_call    & RTS_b_fnp_acc2  ) );         !-1;
            asd:=asd-1;
            addrPARQNT;
      end;


%+PQ  prior
      procedure update_PARQNT(att); short integer att;
      begin short integer basetag;
            basetag:=curpar.ftag;
%+T			STRC2("SCODER2",sourceline,"DUP");
%+T			STRC3("SCODER2",sourceline,"REMOTE",ED_TAG(basetag+quantdisp));
%+T			STRC3("SCODER2",sourceline,"SELECT",ED_TTAG(RTS_parqnt_fp));
%+T			STRC3("SCODER2",sourceline,"PUSHC","C-OADDR " & ED_TAG(basetag+atrdesdisp));
%+T			STRC2("SCODER2",sourceline,"ASSIGN");
%+T			STRC2("SCODER2",sourceline,"DUP");
%+T			STRC3("SCODER2",sourceline,"REMOTE",ED_TAG(basetag+quantdisp));
%+T			STRC3("SCODER2",sourceline,"SELECT",ED_TAG(basetag+att));
            outtext( s_dup & s_remote );         !+1;
            outtag(basetag+quantdisp);
            outtext( s_select & RTS_parqnt_fp &
                     s_pushc  & s_c_oaddr     ); !+1;
            outtag(basetag+atrdesdisp);
            outtext( s_assign &                  !-2;
                     s_dup    & s_remote );      !+1;
            outtag(basetag+quantdisp);
            outbyte(vselect); outTagWithIdent(att);
            asd:=asd+1;
      end;

%title ******   GET_LABQNT, GET_SWTQNT   ******

%+PQ     prior
         procedure get_LABQNT; !*** opq is implicit parameter;
         ! opq.kind=K_label ;
         ! push LABQNT on TOS i.e. stack is asd:=asd+1;
         begin
%+Z            short integer conlvl;
%-Z            character     conlvl;
%-Z %IMPLNONE OFF
            inspect opq do begin
               switch(NUL:C_max) categ begin
                when C_local do begin
                  staticencl;
                  ! If label is inside an inspect statement, this
                    gives a reference to the enclosing 'proper' block;
                  outtext( s_push    & RTS_tmp       &
                           s_select  & RTS_quant_lab &
                           s_select  & RTS_labqnt_sl &
                           s_rupdate & s_pop         &
                           s_pushc   & s_c_paddr     );
                  outtag(ftag+quantdisp);
                  outtext( s_push    & RTS_tmp       &
                           s_select  & RTS_quant_lab &
                           s_select  & RTS_labqnt_pad&
                           s_rupdate & s_pop         &
                           s_pushc   & s_c_int       );
                  conlvl:=conlevel(this quantity); outsint(conlvl);
                  outtext( s_push    & RTS_tmp       &
                           s_select  & RTS_quant_lab &
                           s_select  & RTS_labqnt_clv&
                           s_rupdate & s_pop         &
                           s_push    & RTS_tmp       &
                           s_selectv & RTS_quant_lab );

%                 outtext( s_asscall & RTS_make_lab1 &
%                          s_pushc & s_c_paddr       );   !+1;
%                 outtag(ftag+quantdisp);
%                 outtext( s_asspar                  &
%                          s_pushc & s_c_int         );   !00;
%                 outinteger(conlevel(this quantity));
%                 outtext( s_asspar                  &    !-1;
%                          s_call    & RTS_make_lab2 );   !0;
               end;
               when C_unspec do begin
                  staticencl;
                  outbyte(vremotev); outtag(ftag+quantdisp);
               end;
               when C_virt do begin
                  staticencl;
                  outtext( s_asscall & RTS_mkVlab1 &
                           s_pushc   & s_c_int     );   !+1;
                  outsint(virtno-1);
                  outtext( s_asspar                &    !-1;
                           s_call    & RTS_mkVlab2 );   !0;
               end;
               when C_name do begin
                  addrNAMEQNT;
                  outtext( s_deref   &
                           s_asscall & RTS_EncLab1  &
                           s_call    & RTS_EncLab2  );           !0;
               end;
              end case;
            end inspect opq;
         end get_LABQNT;


         procedure get_SWTQNT; !*** opq is implicit parameter;
         ! opq.kind=K_switch ;
         ! push SWTQNT on TOS , i.e. stack is asd:=asd+1;
         begin
%-Z %IMPLNONE OFF
            inspect opq do begin
               switch(NUL:C_max) categ begin
                when C_local do begin
                  staticencl;
                  outtext( s_push    & RTS_tmp       &
                           s_select  & RTS_quant_swt &
                           s_select  & RTS_swtqnt_sl &
                           s_rupdate & s_pop         &
                           s_pushc   & s_c_oaddr     );
                  outtag(ftag+quantdisp);
                  outtext( s_push    & RTS_tmp       &
                           s_select  & RTS_quant_swt &
                           s_select  & RTS_swtqnt_des&
                           s_rupdate & s_pop         &
                           s_push    & RTS_tmp       &
                           s_selectv & RTS_quant_swt );

%                 outtext( s_asscall & RTS_make_swt1 &
%                          s_pushc   & s_c_oaddr     );   !+1;
%                 outtag(ftag+quantdisp);
%                 outtext( s_asspar                 &     !-1;
%                          s_call   & RTS_make_swt2 );    !0;
               end;
               when C_unspec do begin
                  staticencl;
                  outbyte(vremotev); outtag(ftag+quantdisp);
               end;
               when C_virt do begin
                  staticencl;
                  outtext( s_asscall & RTS_mkVswt1 &
                           s_pushc   & s_c_int     );   !+1;
                  outsint(virtno-1);
                  outtext( s_asspar                &     !-1;
                           s_call    & RTS_mkVswt2 );    !0;
               end;
               when C_name do begin
                  addrNAMEQNT;
                  outtext( s_deref   &
                           s_asscall & RTS_EncSwt1 &
                           s_call    & RTS_EncSwt2 );            !0;
               end;
              end case;
            end inspect opq;
         end get_SWTQNT;


%+T TRC("SCODER2",sourceline,"BEGIN codegenerator2 STATEMENTS");


end codegenerator2;

end SCODER2;
